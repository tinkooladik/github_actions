plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.tinkooladik.githubtest.multimodule'
    compileSdk 34

    defaultConfig {
        minSdk 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

def localPropertyFile = project.rootProject.file('local.properties')
def localProperties = new Properties()
if (localPropertyFile.canRead()) {
    localProperties.load(new FileInputStream(localPropertyFile))
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId "com.tinkooladik"
                artifactId 'github_actions_multimodule'
                version '0.1.6'
            }
        }

        repositories {
            maven {
                name = "GitHubPackagesTest"
                url = uri("https://maven.pkg.github.com/tinkooladik/github_actions")

                credentials {
                    username = localProperties['gpr.user'] ?: System.getenv("GITHUB_ACTOR")
                    password = localProperties['gpr.key'] ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
